#version 460
#extension GL_GOOGLE_include_directive : enable

#include "ray_tracing.glsl"

layout(set = 0, binding = 1) uniform accelerationStructureEXT acc;

layout(set = 0, binding = 2) uniform sampler2D cdepth;
layout(set = 0, binding = 3) uniform sampler2D albedo;
layout(set = 0, binding = 4) uniform sampler2D normal;
layout(set = 0, binding = 5) uniform sampler2D last_cdepth;
layout(set = 0, binding = 6) uniform sampler2D last_albedo;
layout(set = 0, binding = 7) uniform sampler2D last_normal;

layout(set = 0, binding = 8) uniform isampler2D last_step;
layout(set = 0, binding = 9) uniform usampler2D last_acc;
layout(set = 0, binding = 10) uniform sampler2D last_path_next_origin;
layout(set = 0, binding = 11) uniform sampler2D last_path_next_direction;
layout(set = 0, binding = 12) uniform sampler2D last_path_albedo;
layout(set = 0, binding = 13) uniform sampler2D last_path_direct_light;
layout(set = 0, binding = 14) uniform sampler2D last_output;

layout(set = 0, binding = 15, r8i) uniform iimage2D out_step;
layout(set = 0, binding = 16, r16ui) uniform uimage2D out_acc;
layout(set = 0, binding = 17, rgba32f) uniform image2D out_path_next_origin;
layout(set = 0, binding = 18, rgba16f) uniform image2D out_path_next_direction;
layout(set = 0, binding = 19, rgba16f) uniform image2D out_path_albedo;
layout(set = 0, binding = 20, rgba16f) uniform image2D out_path_direct_light;
layout(set = 0, binding = 21, rgba16f) uniform image2D out_output;

layout(location = 0) rayPayloadEXT RayPayload rp;

float rt_depth_to_z(float d)
{
	return il.cam_b / (d -  il.cam_a);
}

float rt_z_to_depth(float z)
{
	return il.cam_b / z + il.cam_a;
}

vec2 rt_ndc_to_ss(vec2 p)
{
	vec2 size = textureSize(albedo, 0);
	return ((p * 0.5) + 0.5) * size;
}

vec3 rt_pos_view(vec2 pos)
{
	vec2 size = vec2(1.0) / textureSize(albedo, 0);
	float d = texelFetch(cdepth, ivec2(pos), 0).x;
	float z = rt_depth_to_z(d);
	vec2 uv = pos * size;
	vec2 ndc2 = (uv - 0.5) * 2.0;
	ndc2 *= il.cam_ratio;
	ndc2 *= z;
	return vec3(ndc2, z);
}

vec3 env_sample_novoid(vec3 dir)
{
	const vec3 hor = vec3(80, 120, 180) / 255;
	const vec3 up = vec3(0, 60, 256) / 255;
	float up_ratio = dot(dir, vec3(0.0, 1.0, 0.0));

	up_ratio = 1.0 - up_ratio;
	up_ratio *= up_ratio;
	up_ratio *= up_ratio;
	up_ratio = 1.0 - up_ratio;
	return normalize(hor * (1.0 - up_ratio) + vec3(1.0) * up);
}

vec3 env_sample(vec3 dir)
{
	return env_sample_novoid(dir) * ((dir.y + 1.0) * 0.5);
}

void main(void)
{
	vec2 fpos = gl_LaunchIDEXT.xy + vec2(0.5);
	ivec2 pos = ivec2(fpos);
	vec3 view = rt_pos_view(fpos);
	vec3 view_norm = normalize(view);
	traceRayEXT(acc,
		gl_RayFlagsOpaqueEXT | gl_RayFlagsCullBackFacingTrianglesEXT,	// rayFlags
		0xFF,	// cullMask
		0,	// sbtRecordOffset
		0,	// sbtRecordStride
		0,	// missIndex
		view,	// origin
		0.001 + rt_depth_to_z(texelFetch(cdepth, pos, 0).x) / il.cam_near,	// Tmin
		il.sun,	// direction
		il.cam_near * 2.0,	// Tmax
		0);	// payload

	vec3 alb = texelFetch(albedo, pos, 0).xyz;
	vec3 norm = texelFetch(normal, pos, 0).xyz;
	float align = dot(norm, il.sun) * (rp.hit ? 0.0 : 1.0);
	vec3 direct_light = alb * max(0.0, align) * 2.5;
	direct_light += alb * env_sample((il.view_normal_inv * vec4(norm, 1.0)).xyz);

	if (texelFetch(cdepth, pos, 0).x == 0.0)
		direct_light = env_sample_novoid((il.view_normal_inv * vec4(view_norm, 1.0)).xyz);
	imageStore(out_output, pos, vec4(direct_light, 1.0));

	/*traceRayEXT(acc,
		gl_RayFlagsOpaqueEXT | gl_RayFlagsCullBackFacingTrianglesEXT,	// rayFlags
		0xFF,	// cullMask
		0,	// sbtRecordOffset
		0,	// sbtRecordStride
		0,	// missIndex
		vec3(0.0),	// origin
		0.001,	// Tmin
		view_norm,	// direction
		il.cam_near * 2.0,	// Tmax
		0);	// payload

	if (rp.hit)
		imageStore(out_output, pos, vec4(rp.albedo, 1.0));
	else
		imageStore(out_output, pos, vec4(vec3(0.0), 1.0));*/
}