#version 460
#extension GL_GOOGLE_include_directive : enable

#include "illum.glsl"
#include "ray_tracing.glsl"

layout(set = 0, binding = 1) uniform accelerationStructureEXT acc;

//#include "rtbp.glsl"
#include "rt.glsl"

layout(set = 0, binding = 2, rgba16f) uniform image2D s_diffuse;
layout(set = 0, binding = 3, r32f) uniform image2D s_diffuse_acc;
layout(set = 0, binding = 4, rgba16f) uniform image2D s_direct_light;
layout(set = 0, binding = 5, r32f) uniform image2D s_direct_light_acc;
layout(set = 0, binding = 6, rgba16f) uniform image2D s_output;

layout(set = 0, binding = 7) uniform sampler2D cdepth;
layout(set = 0, binding = 8) uniform sampler2D albedo;
layout(set = 0, binding = 9) uniform sampler2D normal;
layout(set = 0, binding = 10) uniform sampler2D last_cdepth;
layout(set = 0, binding = 11) uniform sampler2D last_albedo;
layout(set = 0, binding = 12) uniform sampler2D last_normal;

layout(set = 0, binding = 13) uniform sampler2D last_diffuse;
layout(set = 0, binding = 14) uniform sampler2D last_diffuse_acc;
layout(set = 0, binding = 15) uniform sampler2D last_direct_light;
layout(set = 0, binding = 16) uniform sampler2D last_direct_light_acc;

layout(location = 0) rayPayloadEXT RayPayload rp;

vec3 last_pos_view(vec2 pos)
{
	return rt_pos_view(pos, texelFetch(last_cdepth, ivec2(pos), 0).x);
}

float tmin_calc(float z)
{
	return 0.00001 + min((z * 0.1) / il.cam_near, 1.0);
}

float center_divergence(vec2 pos)
{
	vec2 p = abs(fract(pos) - .5);
	return p.x + p.y;
}

void main(void)
{
	vec2 fpos = gl_LaunchIDEXT.xy + vec2(0.5);
	ivec2 pos = ivec2(fpos);
	vec3 view = rt_pos_view(fpos, texelFetch(cdepth, pos, 0).x);
	vec3 view_norm = normalize(view);
	int rnd = (hash(int(gl_LaunchIDEXT.x)) + hash(int(gl_LaunchIDEXT.y))) % 256;

	vec4 last_view = il.view_cur_to_last * vec4(view, 1.0);
	vec2 last_view_pos = rt_project_point(last_view.xyz).xy;
	ivec2 ilast_view_pos = ivec2(last_view_pos);

	const float repr_dist_tres = 0.5;
	float repr_dist = length((il.view_inv * vec4(view, 1.0)).xyz - (il.last_view_inv * vec4(last_pos_view(last_view_pos), 1.0)).xyz);
	bool repr_success = last_view_pos.x >= 0 && last_view_pos.y >= 0 &&
		last_view_pos.x <= (il.size.x) && last_view_pos.y <= (il.size.y) &&
		repr_dist < repr_dist_tres;

	vec3 sun = il.sun;
	vec3 alb = texelFetch(albedo, pos, 0).xyz;
	vec3 norm = texelFetch(normal, pos, 0).xyz;

	vec3 out_output = vec3(0.0);
	vec3 direct_light = vec3(0.0);

	traceRayEXT(acc,
		gl_RayFlagsCullBackFacingTrianglesEXT,	// rayFlags
		0xFF,	// cullMask
		0,	// sbtRecordOffset
		0,	// sbtRecordStride
		0,	// missIndex
		view,	// origin
		tmin_calc(length(view)),	// Tmin
		sun,	// direction
		il.cam_near * 2.0,	// Tmax
		0);
	if (!rp.hit)
		direct_light += max(0.0, dot(norm, sun)) * 2.5;

	vec3 diffuse = vec3(0.0);
	vec3 first_diffuse_dir = rnd_diffuse_around_rough(view_norm, norm, 0.0, rnd);
	{
		vec3 ray_albedo = vec3(1.0);
		vec3 ray_origin = view;
		vec3 ray_dir = first_diffuse_dir;

		for (uint j = 0; j < 4; j++) {
			traceRayEXT(acc,
				gl_RayFlagsCullBackFacingTrianglesEXT,	// rayFlags
				0xFF,	// cullMask
				0,	// sbtRecordOffset
				0,	// sbtRecordStride
				0,	// missIndex
				ray_origin,	// origin
				tmin_calc(length(ray_origin)),	// Tmin
				ray_dir,	// direction
				il.cam_near * 2.0,	// Tmax
				0);
			if (rp.hit) {
				ray_albedo *= rp.albedo;
				ray_origin = rp.pos;
				vec3 dif_normal = (il.view_normal * vec4(rp.normal, 1.0)).xyz;
				ray_dir = rnd_diffuse_around_rough(ray_dir, dif_normal, 0.0, rnd);

				vec3 sun_dir = sun;

				traceRayEXT(acc,
					gl_RayFlagsCullBackFacingTrianglesEXT,	// rayFlags
					0xFF,	// cullMask
					0,	// sbtRecordOffset
					0,	// sbtRecordStride
					0,	// missIndex
					ray_origin,	// origin
					tmin_calc(length(ray_origin)),	// Tmin
					sun_dir,	// direction
					il.cam_near * 2.0,	// Tmax
					0);
				if (!rp.hit)
					diffuse += ray_albedo * max(0.0, dot(sun_dir, dif_normal)) * 2.5;
			} else {
				diffuse += ray_albedo * env_sample_novoid((il.view_normal_inv * vec4(ray_dir, 1.0)).xyz);
				break;
			}
		}
	}

	float acc_diffuse = 1.0;
	float acc_direct_light = 1.0;
	if (repr_success) {
		{
			float last_acc = textureLod(last_diffuse_acc, last_view_pos, 0.0).x;
			if (last_acc > 0.0) {
				vec3 last_diffuse = textureLod(last_diffuse, last_view_pos, 0.0).xyz;
				acc_diffuse = last_acc + 1.0;
				diffuse = (last_diffuse * last_acc + diffuse) / acc_diffuse;
			}
		}
		{
			float last_acc = textureLod(last_direct_light_acc, last_view_pos, 0.0).x;
			if (last_acc > 0.0) {
				vec3 last_direct_light = textureLod(last_direct_light, last_view_pos, 0.0).xyz;
				acc_direct_light = last_acc + 1.0;
				diffuse = (last_direct_light * last_acc + direct_light) / acc_direct_light;
			}
		}
	}

	{
		float div = center_divergence(last_view_pos);
		if (div > 0.01) {
			acc_diffuse *= mix(1.0 - div, 1.0, 0.9);
			acc_direct_light *= mix(1.0 - div, 1.0, 0.8);
		}
	}

	if (texelFetch(cdepth, pos, 0).x == 0.0) {
		out_output = env_sample_novoid((il.view_normal_inv * vec4(view_norm, 1.0)).xyz);
		diffuse = vec3(0.0);
		acc_diffuse = 0.0;
		direct_light = vec3(0.0);
		acc_direct_light = 0.0;
	}

	out_output += (direct_light + diffuse) * alb;
	out_output = diffuse;

	imageStore(s_diffuse, pos, vec4(diffuse, 0.0));
	imageStore(s_diffuse_acc, pos, vec4(acc_diffuse, vec3(0.0)));
	imageStore(s_direct_light, pos, vec4(direct_light, 0.0));
	imageStore(s_direct_light_acc, pos, vec4(acc_direct_light, vec3(0.0)));
	imageStore(s_output, pos, vec4(out_output, 0.0));
}